apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    defaultConfig {
        applicationId "com.example.alex.facebooklayout"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
    }


    def AdsPaid = false
    def AdsFree = true

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def fileName = getParent().getName()
            def flavor = variant.getFlavorName()
            def build = variant.getBuildType().getName()
            fileName = fileName + "_" + build + "_" + flavor + "_" + versionName + ".apk"
            output.outputFile = new File(file.parent, fileName)
            println "Generated apk with name: " + fileName
        }
    }

    flavorDimensions "env", "country"

    productFlavors {
        Paid {
            dimension "env"
            applicationId "com.example.alex.facebooklayout.paid"
            buildConfigField "boolean", "ADS", "$AdsPaid"
        }

        Free {
            dimension "env"
            applicationId "com.example.alex.facebooklayout.free"
            buildConfigField "boolean", "ADS", "$AdsFree"
        }

        France {
            dimension "country"
        }

        China {
            dimension "country"
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])


    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.google.android.gms:play-services-appindexing:9.6.1'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.2'
    testCompile 'org.mockito:mockito-core:1.8.0-rc2'
    compile project(path: ':backend', configuration: 'android-endpoints')
}
